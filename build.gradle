buildscript {

    ext.kotlin_version = "1.3.72"

    ext.buildConfig = [
            'compileSdk': 29,
            'minSdk': 26,
            'targetSdk': 29,
            'versionCode': 1,
            'versionName': "1.0"
    ]

    ext.versions = [
            'lifecycle': '2.2.0',
            'navigation': '2.3.0',
    ]

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:2.3.0"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        
    }
}

subprojects {
    switch (project.name) {
        case "app":
            apply plugin: "com.android.application"
            break
        default:
            apply plugin: "com.android.library"
            break
    }
    apply plugin: "kotlin-android"

    android {
        compileSdkVersion buildConfig.compileSdk
        defaultConfig {
            minSdkVersion buildConfig.minSdk
            targetSdkVersion buildConfig.targetSdk
            versionCode buildConfig.versionCode
            versionName buildConfig.versionName
            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        }
        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            }
        }
        compileOptions {
            sourceCompatibility = JavaVersion.VERSION_1_8
            targetCompatibility = JavaVersion.VERSION_1_8
        }
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    dependencies {

        // Kotlin
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.3"
        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.3"

        // Core
        implementation "androidx.core:core-ktx:1.3.0"

        // Android Test
        androidTestImplementation "androidx.test.ext:junit:1.1.1"
        androidTestImplementation "androidx.test:rules:1.2.0"
        androidTestImplementation "org.assertj:assertj-core:3.2.0"
        androidTestImplementation "io.mockk:mockk-android:1.9.3"

        // Test
        testImplementation("junit:junit:4.12")
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
